buildscript {
    ext.kotlinVersion = '1.0.1'

    repositories { mavenCentral() }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.0.43'
    id 'ru.vyarus.animalsniffer' version '1.1.0'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'

group 'com.github.jk1'
version '0.2.1'

repositories {
    mavenCentral()
    flatDir {
        dirs 'lib'
    }
}

intellij {
    version '15.0.4'              //IntelliJ IDEA 15 dependency
    plugins 'tasks'               //Bundled plugin dependencies
    updateSinceUntilBuild false
    instrumentCode false
    pluginName 'ytplugin'
    publish {
        pluginId '8215'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile 'org.nanohttpd:nanohttpd:2.3.0'
    compileOnly name: 'tasks-api'

    testCompile 'junit:junit:4.12'
    testCompile name: 'tasks-core'

    signature 'org.codehaus.mojo.signature:java17:1.0@signature'
}

test {
    dependsOn animalsnifferMain     // make sure we're java 1.7 compatible
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    outputs.upToDateWhen { false }
}

