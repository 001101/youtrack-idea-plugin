plugins {
    id 'org.jetbrains.intellij' version '0.2.0'
    id 'ru.vyarus.animalsniffer' version '1.2.0'
    id "com.zoltu.kotlin" version "1.0.5"
    id 'idea'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

group 'com.github.jk1'
version '0.6'

repositories {
    mavenCentral()
}

def supportedVersions = ['IC-2016.3', 'LATEST-EAP-SNAPSHOT']

intellij {
    version project.hasProperty('ideaVersion') ? project.ideaVersion : supportedVersions.first()
    plugins 'tasks'  //Bundled plugin dependencies
    updateSinceUntilBuild false
    instrumentCode false
    pluginName 'ytplugin'
    println "This build uses IDEA $version"
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:1.0.1"
    compileOnly "org.jetbrains.kotlin:kotlin-reflect:1.0.1"
    compile 'org.nanohttpd:nanohttpd:2.3.0'

    testCompile 'junit:junit:4.12'

    signature 'org.codehaus.mojo.signature:java17:1.0@signature'
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    outputs.upToDateWhen { false }
}

task testAllSupportedIdes {
    dependsOn supportedVersions.collect { "runTestsWithIdea$it" }
    supportedVersions.forEach { version ->
        task "runTestsWithIdea$version"(type: GradleBuild) {
            buildFile = 'build.gradle'
            tasks = ['test']
            startParameter.projectProperties = [ideaVersion: version]
        }
    }
}

// runs on travis CI
check.dependsOn animalsnifferMain, testAllSupportedIdes

